plugins {
    // Apply shadow to do shaded jars (merge semver)
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'

    // Apply IDE plugins for easy build
    id 'eclipse'

    // For Heisluft
    id 'maven-publish'
    id 'signing'
}

group = 'com.github.antilaby'
version = '2.0-pre3'

sourceCompatibility = 1.8

repositories {
    // For Spigot
    mavenLocal()
    // For GlowStone
    maven {
        url 'https://repo.glowstone.net/content/repositories/snapshots/'
    }
    // For JUnit
    mavenCentral()
    // For ProtocolLib
    maven {
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
    // For HLS
    maven {
        url 'http://heisluft.bplaced.net/maven/'
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand("version": project.version, "name": project.name)
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

dependencies {
    compile 'com.github.zafarkhaja:java-semver:0.10.0-SNAPSHOT'
    compileOnly 'net.glowstone:glowstone:2018.8.0-SNAPSHOT'
    compileOnly('com.comphenix.protocol:ProtocolLib-API:4.4.0-SNAPSHOT') {
        transitive = false
    }
    compileOnly 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    testCompileOnly 'org.junit.jupiter:junit-jupiter-api:5.3.1'
}

publishing {
    repositories {
        maven {
            url = mavenRepo
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
            authentication {
                digest(DigestAuthentication)
            }

        }
    }
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = project.description
                url = 'https://www.spigotmc.org/resources/antilaby-1-8-1-12-1-disable-labymod-functions-api.21347'
                licenses {
                    license {
                        name = 'GNU GPL, Version 3'
                        url = 'https://www.gnu.org/licenses/gpl.txt'
                    }
                }
                developers {
                    developer {
                        id = 'NathanNr'
                        name = 'NathanNr'
                        email = '[n/a]'
                        roles = ['developer']
                    }
                    developer {
                        id = 'heisluft'
                        name = 'heisluft'
                        email = 'heisluftlp@gmail.com'
                        roles = ['developer']
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/AntiLaby/AntiLaby.git'
                    developerConnection = 'scm:git:ssh://git@github.com/AntiLaby/AntiLaby.git'
                    url = 'https://github.com/AntiLaby/AntiLaby'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

publish.dependsOn test

test {
    useJUnit()
}

build {
    doLast {
        test
    }
}

task copyToServer(type: Copy, dependsOn: jar) {
    def ts = System.getProperty("serverDir")
    if (ts == null) return
    ts.replace('\\', '/')
    ts += (!ts.endsWith('/') ? '/' : "").concat("plugins")
    from shadowJar
    into ts
}