version = "pre-2.0"
// Apply the java-library plugin to add support for Java Library
apply plugin: 'java'

// Apply IDE plugins for easy build
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    // For Spigot
    mavenLocal()
    // For JUnit
    mavenCentral()
    // For ProtocolLib
    maven {
        url "http://repo.dmulloy2.net/content/groups/public/"
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs)
    exclude 'plugin.yml'
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand("version": project.version, "name": project.name)
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    compile 'com.comphenix.protocol:ProtocolLib:4.4.0-SNAPSHOT'
    compile 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    testCompile 'junit:junit:4.12'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

task buildAll() {}

buildAll.dependsOn jar, javadocJar, sourcesJar

task copyJarToTestserver(type: Copy) {
    pathToTestServer = pathToTestServer.replace('\\', '/')
    if (!pathToTestServer.endsWith("/")) pathToTestServer += '/'
    from jar
    into pathToTestServer.concat('plugins')
}