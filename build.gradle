plugins {
    // Apply shadow to do shaded jars (merge semver)
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'

    id 'checkstyle'

    // Apply IDE plugins for easy build
    id 'eclipse'

    // For Heisluft
    id 'maven-publish'
    id 'signing'
}

group = 'com.github.antilaby'
version = '2.0-pre3'

sourceCompatibility = 1.8

repositories {
    // For Spigot
    mavenLocal()
    // For GlowStone
    maven {
        url 'https://repo.glowstone.net/content/repositories/snapshots/'
    }
    //Bukkit API
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    // For JUnit
    mavenCentral()
    // For ProtocolLib
    maven {
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
    // For JSemVer
    maven {
        url 'http://heisluft.bplaced.net/maven/'
    }
    //For bstats
    maven {
        url 'https://repo.codemc.org/repository/maven-public'
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand("version": project.version, "name": project.name)
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource, file("LICENSE")
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

shadowJar {
    relocate 'org.bstats', 'com.github.antilaby.3rdparty.bstats'
}

jar {
    from classes, processResources, file("LICENSE")
}

dependencies {
    compile 'com.github.zafarkhaja:java-semver:0.10.0-SNAPSHOT'
    compile 'org.bstats:bstats-bukkit:1.4'
    compileOnly 'net.glowstone:glowstone:2018.8.0-SNAPSHOT'
    compileOnly('com.comphenix.protocol:ProtocolLib-API:4.4.0-SNAPSHOT') {
        transitive = false
    }
    testCompileOnly 'net.glowstone:glowstone:2018.8.0-SNAPSHOT'
    compileOnly 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    testCompileOnly 'org.junit.jupiter:junit-jupiter-api:5.3.1'
}

configurations.testCompileOnly.extendsFrom configurations.compileOnly

publishing {
    repositories {
        maven {
            url = mavenRepo
            credentials {
                username = mavenUsername
                password = mavenPassword
            }
            authentication {
                digest(DigestAuthentication)
            }

        }
    }
    publications {
        mavenJava(MavenPublication) {
            pom {
                description = project.description
                url = 'https://www.spigotmc.org/resources/antilaby-1-8-1-12-1-disable-labymod-functions-api.21347'
                licenses {
                    license {
                        name = 'GPL-3.0'
                        url = 'https://www.gnu.org/licenses/gpl.txt'
                    }
                }
                developers {
                    developer {
                        id = 'NathanNr'
                        name = 'NathanNr'
                        email = 'nathan.nr@outlook.com'
                        roles = ['developer']
                    }
                    developer {
                        id = 'heisluft'
                        name = 'heisluft'
                        email = 'heisluftlp@gmail.com'
                        roles = ['developer']
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/AntiLaby/AntiLaby.git'
                    developerConnection = 'scm:git:ssh://git@github.com/AntiLaby/AntiLaby.git'
                    url = 'https://github.com/AntiLaby/AntiLaby'
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

publishMavenJavaPublicationToMavenRepository.dependsOn test
publishMavenJavaPublicationToMavenLocal.dependsOn test

classes {
    doLast {
        checkstyleMain
    }
}

testClasses {
    doLast {
        checkstyleTest
    }
}

test {
    useJUnit()
}

build {
    doLast {
        test
    }
}

task copyToServer(type: Copy, dependsOn: jar) {
    def ts = System.getProperty("serverDir")
    if (ts == null) return
    ts.replace('\\', '/')
    ts += (!ts.endsWith('/') ? '/' : "").concat("plugins")
    from shadowJar
    into ts
}
