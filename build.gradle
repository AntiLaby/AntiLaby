group = 'com.github.antilaby'
version = '2.0-pre1'

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java'

// Apply IDE plugins for easy build
apply plugin: 'eclipse'

// For Heisluft
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

repositories {
    // For Spigot
    mavenLocal()
    // For JUnit
    mavenCentral()
    // For ProtocolLib
    maven {
        url 'http://repo.dmulloy2.net/content/groups/public/'
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand("version": project.version, "name": project.name)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
    compile('com.comphenix.protocol:ProtocolLib-API:4.4.0-SNAPSHOT') {
        exclude module: 'BukkitExecutors'
        exclude module: 'cglib-nodep'
    }
    compile 'org.spigotmc:spigot:1.12.2-R0.1-SNAPSHOT'
    testCompile 'junit:junit:4.12'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            url "file://" + System.getProperty("user.home") + "/.m2/repository/"
        }
    }
    publications {
        testing(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

publish.dependsOn jar, sourcesJar, javadocJar

task copyToServer(type: Copy, dependsOn: jar) {
    def ts = System.getProperty("serverDir")
    if (ts != null) {
        ts.replace('\\', '/')
        ts += (!ts.endsWith('/') ? '/' : "").concat("plugins")
        from jar
        into ts
    }
}